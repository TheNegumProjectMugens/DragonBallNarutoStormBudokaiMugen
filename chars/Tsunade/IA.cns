;===============================================================================
; INTELIGENCIA ARTIFICIAL:
;===============================================================================
[Statedef -2]

[State 0, Gotas]
type = Explod
triggerall = var(2) = 1
trigger1 = timemod = 7,0
anim = 1
ID = 1
pos = -15+random%35,0-random%50
postype = p1
facing = ifelse(random < 500,1,-1)
vfacing = 1
bindtime = 1
vel = -.5,-1.5
removetime = -2
scale = .25,.25
sprpriority = 2
ontop = 1
ownpal = 1
supermovetime = 999

[State 0, PalFXWiz]
type = PalFX
triggerall = var(2) = 1
trigger1 = timemod = 10000,0
trigger2 = time = 1
time = 10000
add = 0,12,25
mul = 256,256,256
sinadd = 20,20,20,20
invertall = 0
color = 256
ignorehitpause = 1

[State 0, StopSnd]
type = StopSnd
trigger1 = prevstateno = 190 && time = 1
channel = 10
[State 0, StopSnd]
type = StopSnd
trigger1 = prevstateno = 190 && time = 1
channel = 11

[State 0, RemoveExplod]
type = RemoveExplod
trigger1 = prevstateno = 310 && time = 1
id = 315

[State 0, Run]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x > 100
trigger1 = random < (ailevel * 100)/2
value = ifelse(pos y >= 0,60,65)

[State 0, Run Atras]
type = ChangeState
triggerall = var(5) = 0
triggerall = stateno != 60
triggerall = stateno != 65
triggerall = stateno != 70
triggerall = backedgebodydist > 50
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 100 && p2movetype = A
trigger1 = random < (ailevel * 100)/4
value = 70

[State 0, Guard]
type = ChangeState
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = (p2bodydist x < 50 && p2movetype = A) || (enemy,numproj > 0)
trigger1 = random < (ailevel * 100)/2
value = ifelse(statetype = A,132,130)

; BYAKUGO MODE:

[State 0, 4 BYAKUGO MODE]
type = ChangeState
triggerall = power >= 3000
triggerall = var(2) = 0
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x >= 100
trigger1 = random < (ailevel * 100)*2
value = 800

; GOLPES NORMALES:

[State 0, A,B & C]
type = ChangeState
triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x < 30
trigger1 = random < (ailevel * 100)
value = ifelse(random < 500,200,ifelse(random < 500,300,ifelse(var(2) = 1,2400,400)))

[State 0, Air Combo]
type = ChangeState
triggerall = statetype = A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl
triggerall = p2bodydist x = [-30,30]
trigger1 = random < (ailevel * 100)
value = 600

; SPECIALS:

[State 0, Jisuberi]
type = ChangeState
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
trigger1 = p2bodydist x = [50,100]
trigger1 = random < (ailevel * 100)/2
trigger1 = var(2) = 0
trigger2 = p2bodydist x = [-100,100]
trigger2 = random < (ailevel * 100)/2
trigger2 = var(2) = 1
value = 1000

[State 0, Iryo Ninjutsu]
type = ChangeState
triggerall = life < lifemax
triggerall = var(6) < 3
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x >= 100
trigger1 = life < lifemax/2 && random < (ailevel * 100)/2
trigger2 = life < lifemax/4 && random < (ailevel * 100)
value = 1100

[State 0, Solid Gold Fist]
type = ChangeState
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
trigger1 = p2bodydist x < 50
trigger1 = random < (ailevel * 100)/2
trigger1 = var(2) = 0
trigger2 = p2bodydist x = [75,150]
trigger2 = random < (ailevel * 100)/2
trigger2 = var(2) = 1
value = 1200

[State 0, Kuchiyose Katsuyu Zesshi Nensan]
type = ChangeState
triggerall = numhelper(1320) = 0
Triggerall = power >= 2000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [100,300]
trigger1 = random < (ailevel * 100)*1.25
value = 1300

[State 0, Tsutenkyaku]
type = ChangeState
Triggerall = power >= 1000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x = [75,150]
trigger1 = random < (ailevel * 100)/2
value = 1400

[State 0, Gokuraku Jodo]
type = ChangeState
Triggerall = power >= 2000
Triggerall = statetype != A
triggerall = ailevel > 0 && roundstate = 2
triggerall = ctrl || numhelper(700) > 0
triggerall = p2bodydist x < 50
trigger1 = random < (ailevel * 100)*1.25
value = 1500
;===============================================================================
;---------------------------Estados usados Siempre------------------------------
;===============================================================================
[Statedef -3]
[State 0, Helper de Conexiones]
type = Helper
Triggerall = numhelper(700) = 0
trigger1 = stateno = 200 && movecontact
trigger2 = stateno = 210 && movecontact
trigger3 = stateno = 221 && movecontact
trigger4 = stateno = 230 && time > 36
trigger5 = stateno = 300 && movecontact
trigger6 = stateno = 321 && time > 5
trigger7 = stateno = 330 && time > 30
trigger8 = stateno = 400 && movecontact
trigger9 = stateno = 410 && movecontact
trigger10 = stateno = 420 && time > 50
trigger11 = stateno = 1000 && time > 90
trigger12 = stateno = 1200 && time > 64
trigger13 = stateno = 1300 && time > 120
trigger14 = stateno = 1401 && (ifelse(var(2) = 1,time > 73,time > 33))
trigger15 = stateno = 1500 && time > 65
trigger16 = stateno = 1501 && time > 40
helpertype = normal
name = "Helper de Conexiones"
ID = 700
stateno = 700
pos = 999999,999999
postype = p1

[State 0, Helper Pared]
type = Helper
trigger1 = enemy,stateno = 236 && numhelper(240) = 0
helpertype = normal
name = "Helper Pared"
ID = 240
stateno = 240
pos = 0,-30
postype = p2
facing = 1

[State 0, Helper Polvo]
type = Helper
triggerall = p2movetype = H
trigger1 = enemy,stateno = 235 && numhelper(245) = 0
helpertype = normal
name = "Helper Polvo"
ID = 245
stateno = 245
pos = 0,-40
postype = p2
facing = 1
;===============================================================================
; Air Jump y Limitador Air Jump:
;-------------------------------------------------------------------------------
[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 0 && stateno = 40 && prevstateno = 50
trigger2 = var(5) = 0 && stateno = 40 && prevstateno = [60,70]
v = 5
value = 1

[State 0, VarSet]
type = VarSet
trigger1 = var(5) = 1 && pos y >= 0
v = 5
value = 0

[State 0, Air Jump]
type = ChangeState
triggerall = stateno = 50
triggerall = pos y < -20
triggerall = var(5) = 0
triggerall = time > 10
trigger1 = ailevel = 0 && command = "up"
trigger2 = roundstate = 2 && ailevel > 0 && random < (ailevel * 100) && p2bodydist y < - 30
trigger3 = roundstate = 2 && ailevel > 0 && random < (ailevel * 100)/8
value = 40
ctrl = 1
;===============================================================================
; GUARD SPARK:
;-------------------------------------------------------------------------------
[State 0, Guard Spark]
type = Explod
triggerall = numexplod(7010) = 0
trigger1 = moveguarded = 1
anim = 7010
ID = 7010
postype = P2
pos = 8,-30
facing = 1
sprpriority = 2
supermove = 1
removetime = -2
bindtime = 1
ownpal = 1
scale = .6,.6
removeongethit = 1
ontop = 1
ignorehitpause = 1
;===============================================================================
; Byakugo Mode:
;-------------------------------------------------------------------------------
[State -3, VarSet - Byakugo Mode]
type = VarSet
trigger1 = numhelper(850) = 1
v = 2
value = 1

[State 0, Fin Forzado]
type = ChangeState
triggerall = var(2) = 1
trigger1 = numhelper(850) = 0
trigger1 = stateno = [0,20]
value = 825

[State -3, VarSet, Golpeado]
type = VarSet
trigger1 = var(2) = 1
v = 10
value = 1000

[State -3, VarSet, Apariencia]
type = VarSet
trigger1 = var(2) = 1
v = 11
value = 2000

[State 0, LifeAdd]
type = LifeAdd
triggerall = life > 10
trigger1 = var(2) = 1
trigger1 = timemod = 3,0
value = 1
;-------------------------------------------------------------------------------
; COMUNES:
[State -3, VarSet,Apariencia]
type = VarSet
trigger1 = var(2) = 0
v = 11
value = 0

[State -3, VarSet,Golpeado]
type = VarSet
trigger1 = var(2) = 0
v = 10
value = 0

[State 0, AttackMulSet]
type = AttackMulSet
trigger1 = 1
value = 1+fvar(2)

[State 0, DefenceMulSet]
type = DefenceMulSet
trigger1 = 1
value = 1-fvar(2)
;-------------------------------------------------------------------------------
; FVARS:
;-------------------------------------------------------------------------------
[State 0, VarSet] ; Base
type = VarSet
trigger1 = var(2) = 0
fv = 2
value = 0

[State 0, VarSet] ; Byakugo Mode
type = VarSet
trigger1 = var(2) = 1
fv = 2
value = .1
